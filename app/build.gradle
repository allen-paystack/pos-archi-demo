apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdk
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName  rootProject.ext.versionName
        versionCode  rootProject.ext.versionCode
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "hardware", "processor"

    productFlavors {
        nexgo {
            dimension "hardware"
            applicationIdSuffix ".nexgo"
        }
        huaweii {
            dimension "hardware"
            applicationIdSuffix ".huaweii"
        }
        epms {
            dimension "processor"
        }
        tams {
            dimension "processor"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    def ext = rootProject.ext

    implementation project(ext.posContract)
    nexgoImplementation project(ext.nexgo)
    huaweiiImplementation project(ext.huaweii)
    epmsImplementation project(ext.epms)
    tamsImplementation project(ext.tams)

    def dependency = ext.appDependencies

    implementation dependency.multiDex
    implementation dependency.coreKtx
    implementation dependency.appCompat
    implementation dependency.materialDesign
    implementation dependency.constraintLayout
    implementation dependency.navigationFragment
    implementation dependency.navigationUI

    def testDependency = ext.testDependencies

    testImplementation testDependency.junit
    androidTestImplementation testDependency.junitAndroid
    androidTestImplementation testDependency.espressoCore
}
